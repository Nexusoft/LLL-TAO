#!/bin/tcsh
#
# RL - Restart LISP Wrapper Script
#
# Nexus/LISP wrapper script to start the lispers.net LISP subsystem. The
# lisp.config configuration file will be provisioned with parameters in
# variables at the top of this file.
#
#------------------------------------------------------------------------------

#
# Comment this line out if you want to assign static EIDs. Otherwise random
# values will be generated in instance-ID 0. Provisioning is only performed
# when the lisp.config file does not exist in this directory.
#
set random

#
# Provision instance-ID and EIDs if not already provisioned.
#
egrep "<iid>" lisp.config >& /dev/null
if ($status != 1) then
  if ($?random) then
    set rand = `python -c "import random; print random.randint(0, 0xffffff)"`
    set byte2 = `python -c "print ($rand >> 16) & 0xff"`
    set byte1 = `python -c "print ($rand >> 8) & 0xff"`
    set byte0 = `python -c "print $rand & 0xff"`
    set v4_eid = 240.$byte2.$byte1.$byte0
    set v6_eid = fe::${byte2}:${byte1}:${byte0}
#   set iid = 0
    set iid = 1537
  else
    set v4_eid = 240.0.255.255
    set v6_eid = fd::fd
    set iid = 1537
  endif
  set hostname = `hostname`

  echo "Provisioning lisp.config file with EIDs [$iid]$v4_eid & [$iid]$v6_eid"
  cat lisp.config.xtr | sed "s/<sample>/$hostname/" > lisp.config.bak
  cat lisp.config.bak | sed "s/<iid>/$iid/" > lisp.config
  cat lisp.config | sed "s/<v4-eid>/$v4_eid/" > lisp.config.bak
  cat lisp.config.bak | sed "s/<v6-eid>/$v6_eid/" > lisp.config
  rm lisp.config.bak
else
  echo "lisp.config file already provisioned"
endif

#
# Configure both EIDs on interface lo.
#
sudo ip addr add $v4_eid/32 dev lo >& /dev/null
sudo ip addr add $v6_eid/128 dev lo >& /dev/null

#
# Packets destined to EIDs and multicast groups are sourced from EIDs. 
#
sudo ip route add 240.0.0.0/8 dev lo src $v4_eid
sudo ip route add 224.0.0.0/4 dev lo src $v4_eid
sudo ip route add fd::/8 dev lo src $v6_eid
sudo ip route add ff::/8 dev lo src $v6_eid

#
# Start LISP up.
#
/lispers.net/RESTART-LISP 8080 eth0
